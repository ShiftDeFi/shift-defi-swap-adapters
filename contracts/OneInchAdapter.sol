// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;

import {ReentrancyGuard} from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import {ISwapAdapter} from "@shift-defi/core/interfaces/ISwapAdapter.sol";
import {IOneInchAdapter} from "./interfaces/IOneInchAdapter.sol";
import {IOneInchV6} from "./dependencies/1inch/IOneInchV6.sol";

contract OneInchAdapter is ReentrancyGuard, ISwapAdapter, IOneInchAdapter {
    using SafeERC20 for IERC20;

    address public immutable oneInchRouter;

    constructor(address _oneInchRouter) {
        oneInchRouter = _oneInchRouter;
    }

    /// @inheritdoc ISwapAdapter
    function swap(
        address tokenIn,
        address tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address receiver,
        bytes memory data
    ) external payable override nonReentrant {
        IERC20(tokenIn).safeTransferFrom(msg.sender, address(this), amountIn);
        IERC20(tokenIn).forceApprove(oneInchRouter, amountIn);

        uint256 balanceBefore = IERC20(tokenOut).balanceOf(address(this));

        (bytes4 selector, bytes memory parameters) = abi.decode(data, (bytes4, bytes));
        if (selector == IOneInchV6.swap.selector) {
            (address executor, IOneInchV6.SwapDescription memory descs, bytes memory executorData) = abi.decode(
                parameters,
                (address, IOneInchV6.SwapDescription, bytes)
            );

            require(descs.srcToken == tokenIn, InvalidSourceToken(descs.srcToken));
            require(descs.dstToken == tokenOut, InvalidDestinationToken(descs.dstToken));
            require(descs.amount == amountIn, InvalidAmountIn(descs.amount));
            require(descs.minReturnAmount <= minAmountOut, InvalidMinAmountOut(descs.minReturnAmount));
            require(descs.dstReceiver == address(this), InvalidSrcReceiver(descs.srcReceiver));
            IOneInchV6(oneInchRouter).swap(executor, descs, executorData);
        } else {
            revert InvalidSelector(selector);
        }

        uint256 balanceAfter = IERC20(tokenOut).balanceOf(address(this));
        uint256 deltaTokenOut = balanceAfter - balanceBefore;
        require(deltaTokenOut >= minAmountOut, SlippageNotMet(tokenOut, deltaTokenOut, minAmountOut));
        IERC20(tokenOut).safeTransfer(receiver, deltaTokenOut);
    }
}
